plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.8"
}

group = 'de.mcterranova'
version = '1.0.0'

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'

    repositories {
        mavenCentral()

        maven {
            name = 'papermc-repo'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'citizens-repo'
            url = 'https://maven.citizensnpcs.co/repo'
        }
        maven {
            name = "codemc-repo"
            url = "https://repo.codemc.org/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = 'enginehub-repo'
            url = 'https://maven.enginehub.org/repo/'
        }
        maven {
            name = "Oraxen"
            url = "https://repo.oraxen.com/releases"
        }
        maven {
            name = "github"
            url = "https://maven.pkg.github.com/TerraNova-Devs/TerranovaLib"
            credentials {
                // You might store these in gradle.properties or use env variables
                username = project.findProperty("githubUser") ?: ""
                password = project.findProperty("githubToken") ?: ""
            }
        }
    }

    dependencies {
        implementation("com.zaxxer:HikariCP:5.0.1")
    }

    def targetJavaVersion = 21
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
        }
        sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
        targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    // Ensure shadow jar is built on 'build'
    tasks.named("build") {
        dependsOn "shadowJar"
    }
}
